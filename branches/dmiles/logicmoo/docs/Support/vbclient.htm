<html>
<head>
<title>Microsoft Virtual Worlds</title>
<link rel=stylesheet href=../shared/global.css>
<object id=theclient classid=clsid:D4898803-20EF-11D0-8A96-00A0C9082583></object>
<OBJECT id=AvatarAccessControl style="LEFT: 0px; TOP: 0px" classid="clsid:AF0AD5CC-418B-11D1-BB2B-00C04FC2CAB1">
	<PARAM NAME="_Version" VALUE="65536">
	<PARAM NAME="_ExtentX" VALUE="2646">
	<PARAM NAME="_ExtentY" VALUE="1323">
	<PARAM NAME="_StockProps" VALUE="0"></OBJECT>
<OBJECT id=webhelper style="LEFT: 0px; TOP: 0px" classid="clsid:7886A636-606B-11d1-BB36-00C04FC2CAB1">
	<PARAM NAME="_Version" VALUE="65536">
	<PARAM NAME="_ExtentX" VALUE="2646">
	<PARAM NAME="_ExtentY" VALUE="1323">
	<PARAM NAME="_StockProps" VALUE="0"></OBJECT>
<script language=vbs>

option explicit

public avatarName, oldSelectedIndex
public iDrawFastWhenMoving, iColorModel, iUseHWAcceleration, bForceBackBuffering, bAutoDownloadAvatars
public bPlaySoundFX, bShowGestures, bShowEvents, bShowSysMsgs

avatarName = ""

'===========================================
' Window load & unload subs

sub window_OnLoad
	AvatarList.focus
	set AvatarAccessControl.VWClient = theclient.vwclient
	RefreshAvatarList
	if AvatarList.length < 2 then
		DisableControls
	elseif AvatarList.value = "" then
		DisableControls
	end if
	LoadPrefs
	if screen.width < 800 or screen.height < 600 then
		msgbox "This Virtual Worlds client may not display properly at resoultions less than 800x600.", vbExclamation, "Microsoft Virtual Worlds"
	end if
	if AvatarList.length <= 1 then
		MsgBox "Before using Virtual Worlds, you must create an avatar.", vbInformation, "Microsoft Virtual Worlds"
		OnCreateAvatar
	end if
end sub

sub window_OnUnload
	on error resume next

	set AvatarAccessControl.VWClient = Nothing

	dim VWClient
	set VWClient = theclient.VWClient

	if Not VWClient Is Nothing then
		if Not VWClient.World Is Nothing then
			VWClient.World.disconnect
		end if

		VWClient.Disconnect
	end if
end sub

'===========================================
' OnOptions
'
sub OnOptions
	dim retDlg
	retDlg = window.showModalDialog("options.htm", top, "dialogHeight:354px;dialogWidth:412px;status:no;help:no")
end sub

'===========================================
' OnDirectory
'
sub OnDirectory()
	dim sDSPage, retDlg
	sDSPage = ""
	dim world
	set world = theclient.vwclient.world
	if typename(world) = "IWorld" then
		if world.global.isvalidproperty("DirectoryServicePage") = true then
			sDSPage = world.global.DirectoryServicePage
		end if
	end if
	if sDSPage <> "" then
		retDlg = window.showModalDialog(sDSPage, window, "dialogHeight:327px;dialogWidth:512px;status:no;help:no")
	else
		msgbox "Worlds Directory Services are not enabled in this Virtual Worlds client.", vbInformation, "Microsoft Virtual Worlds"
	end if
end sub

sub LoadPrefs
	' 1) Set the defaults
	iUseHWAcceleration = -1
	bForceBackBuffering = False
	iDrawFastWhenMoving = -1
	iColorModel = -1
	bAutoDownloadAvatars = true
	bPlaySoundFX = true
	bShowGestures = true
	bShowEvents = true
	bShowSysMsgs = false

	' 2) Try to load the cookie file
	LoadPrefsFromCookie
end sub


sub document_OnKeyDown
	if window.event.keycode = 13 then
		if window.event.srcElement.id = "LastWorld" then
			OnConnect
		elseif window.event.srcElement.id = "NewWorld" then 
			OnConnectToNewWorld
		end if
	end if
end sub


'===========================================
' OnCreateAvatar
' Opens dialog so user can enter avatar name
sub OnCreateAvatar
	dim retDlg
	retDlg = window.showModalDialog("avatar.htm", window, "dialogHeight:134px;dialogWidth:292px;status:no;help:no")
end sub

'===========================================
' OnDeleteAvatar
' Deletes that avatar from the list
sub OnDeleteAvatar
	dim retDlg
	retDlg = window.showModalDialog("delava.htm", window, "dialogHeight:100px;dialogWidth:324px;status:no;help:no")
end sub

sub OnConfirmDelete
	dim iCurAvatar
	iCurAvatar = avatarlist.selectedIndex
	AvatarAccessControl.DeleteAvatar
	RefreshAvatarList
	RedrawPage ""
	'Now, set current avatar to be the next one on the list
	'If the deleted avatar was the last one, select the first of element of the list
	if iCurAvatar < AvatarList.Length - 1 then
		AvatarList.selectedIndex = iCurAvatar
	else
		AvatarList.selectedIndex = 0
	end if
	AvatarList_OnChange
end sub

sub DeleteBrokenAvatar(deadName)
	dim theResult, iCurAvatar
	iCurAvatar = avatarlist.selectedIndex
	theResult = AvatarAccessControl.DeleteDeadAvatar(deadName)
	if theResult = true then
		RefreshAvatarList
		RedrawPage ""
		'Now, set current avatar to be the next one on the list
		'If the deleted avatar was the last one, select the first of element of the list
		if iCurAvatar < AvatarList.Length - 1 then
			AvatarList.selectedIndex = iCurAvatar
		else
			AvatarList.selectedIndex = 0
		end if
		AvatarList_OnChange
	else
		msgbox "Virtual Worlds could not delete the broken avatar.", vbCritical, "Microsoft Virtual Worlds"
		AvatarAccessControl.DisconnectAvatar
		DisableUI
	end if
end sub

'===========================================
' RefreshAvatarList
' repopulates the avatar list from the regkeys

sub RefreshAvatarList
	dim el, curName, i
	for i = 0 to AvatarList.length
		AvatarList.remove(0)
	next
	for each curName in AvatarAccessControl.avatarList
		set el = document.CreateElement("OPTION")
		el.value = curName
		el.text = curName
		AvatarList.add el, PosInList(AvatarList, el.text)
	next
	'Last, create default new user entry
	set el=document.CreateElement("OPTION")
	el.value = "(new avatar)"
	el.text = "(new avatar)"
	AvatarList.add el
end sub

'===========================================
' isInSelectList
' checks if name is contained within a list
function isInSelectList
	dim i
	isInSelectList = false
	for i = 0 to AvatarList.length - 1
		if AvatarList.options(i).value = avatarName then
			isInSelectList = true
			exit for
		end if
	next
end function

'===========================================
' OnDoneCreatingAvatar
' Checks to make sure avatar's name is valid, then
' creates a new avatar, refreshes the menu,
' and makes the avatar the current avatar
function OnDoneCreatingAvatar
	' verify the name...
	dim bReturnVal
	bReturnVal = true
	if avatarName <> "" then
		' Check to make sure length is between 3 and 12 and no spaces or &'s
		' First case: name is OK
		if not isInSelectList and checkLength(avatarName) and checkChars(avatarName) then
				if AvatarAccessControl.CreateNewAvatar(avatarName, "") = true then
					RefreshAvatarList
					AvatarList.value = avatarName
					if AvatarList.value = avatarName then
						avatarName = ""
						AvatarList_OnChange
						PopulateDestinationWorldsList
						' now, add a key that shows this avatar has never connected
						AvatarAccessControl.currentUser.Profile.AddKey "FirstTimeUser", true
					else
						MsgBox "Unable to create an avatar.  Try using a different avatar name.  If this error persists, check your Virtual Worlds installation.", vbExclamation, "Microsoft Virtual Worlds"
						bReturnVal = false
						'OnCreateAvatar
					end if
				else
					MsgBox "Unable to create an avatar.  Try using a different avatar name.  If this error persists, check your Virtual Worlds installation.", vbExclamation, "Microsoft Virtual Worlds"
					bReturnVal = false
				end if
		elseif isInSelectList then	'Avatar is already in list
			MsgBox "This avatar nickname already exists.  Choose a different nickname.", vbInformation, "Microsoft Virtual Worlds"
			bReturnVal = false
			'OnCreateAvatar
		elseif not checkChars(avatarName) then	'Length OK but characters not
			msgbox "An avatar nickname can not contain a space, a tab, or any of the following characters:" & vbcrlf & " / \ : ? "" < > | & " & vbcrlf & "Enter a new nickname.", vbExclamation, "Microsoft Virtual Worlds"
			bReturnVal = false
			'OnCreateAvatar
		else	'Third: Length not OK but char's fine
			MsgBox "Avatar nicknames must be between 2 and 12 characters in length. Enter a new nickname.", vbInformation, "Microsoft Virtual Worlds"
			bReturnVal = false
			'OnCreateAvatar
		end if
	else
		DisableControls
		AvatarList.selectedIndex = -1
	end if
	OnDoneCreatingAvatar = bReturnVal
end function

'===========================================
' Disable  controls
sub DisableControls()
	LastWorld.value = ""
	LastWorld.disabled = true  'changed 1.19.00
	NewWorld.disabled = true
	DisableButton btnDelete
	DisableButton btnOptions
	DisableButton btnDirectory
end sub

'===========================================
' Enable controls
sub EnableControls()
	LastWorld.disabled = false 'changed 1.19.00
	NewWorld.disabled = false
	EnableButton btnDelete
	EnableButton btnOptions
	EnableButton btnDirectory
end sub

sub DisableButton(btn)
	btn.disabled=true
	btn.className = "bDisabled"
end sub

sub EnableButton(btn)
	btn.disabled=false
	btn.className = "bEnabled"
end sub

sub enableUI
	on error resume next
	EnableControls
	' recent worlds
	PopulateDestinationWorldsList
	' install default value - this SHOULD be in the Recent Worlds list
	LastWorld.value = AvatarAccessControl.currentUser.profile.KeyValue("LastWorld")
	err.clear
end sub

'===========================================
' RedrawPage (newUser)
' given the user object, redraws the page.
' Correctly handlesthe case of a NULL user
sub RedrawPage(newUserName)
	if newUserName <> "" then
		EnableUI
	else
		' disable everything
		DisableControls
	end if
end sub

'===========================================
' Populate combo box control with
' recent destination worlds based on
' current avatar
sub PopulateDestinationWorldsList
	dim el, theList, i
	set theList = AvatarAccessControl.currentUser.profile.KeyValue("RecentWorlds")
	
	'clear select list of destination worlds
	for i = 0 to LastWorld.Length - 1
		LastWorld.Remove(0)
	next

	
	'add the new items
	for i = 0 to theList.Count-1
		set el = document.CreateElement("OPTION")
		el.value = theList.Property(i)
		el.text = theList.Property(i)
		LastWorld.add el
	next

end sub


'===========================================
' AvatarList_OnChange
' Called when the user selects a new avatar from
' the avatar list.
sub AvatarList_OnChange
	dim theResult
	'Toggle between values so onchange event is fired when new avatar is selected 2x in a row
	if trim(AvatarList.value) = "(new avatar)" then
		'Set focus away from new avatar
		AvatarList.selectedIndex = oldSelectedIndex
		' create a new User
		OnCreateAvatar
	elseif AvatarList.value <> "" then
		if AvatarAccessControl.ConnectAvatar(avatarList.value, "") = false then
			theResult = MsgBox("The selected avatar could not be loaded. It may be defective. Delete defective avatar now?", vbYesNo + vbQuestion, "Microsoft Virtual Worlds")
			if theResult = vbYes then
				DeleteBrokenAvatar avatarList.value
			else
				AvatarAccessControl.DisconnectAvatar
				RedrawPage ""
			end if
		else
			oldSelectedIndex = AvatarList.selectedIndex
			RedrawPage AvatarList.value
		end if
	else
		AvatarAccessControl.DisconnectAvatar
		RedrawPage AvatarList.value
	end if
end sub

'===========================================
' AvatarList_OnClick
' Handles case of clean build when "(new avatar)" is only option
sub AvatarList_OnClick
	if AvatarList.length = 1 then
		OnCreateAvatar
	end if
end sub

'===========================================
' LogonFromDS(vws_url)
' If necessary, creates new option of vws_url in LastWorld select list
' and logs onto that world

function LogonFromDS(vws_url)
	dim index, el
	
	set index = LastWorld.options.item(vws_url)
	if index is nothing then
		set el = document.CreateElement("OPTION")
		el.value = vws_url
		el.text = vws_url
		LastWorld.add el
	end if
	
	LastWorld.Value = vws_url
	OnConnect
	LogonFromDS = true
end function

'===========================================
' OnConnect
' Connects avatar to previously visited world
Sub OnConnect
	dim curWorldname
	curWorldname = trim(LastWorld.Value)

	'First, check for valid avatar and world name
	if CheckAvatarListValue = false then
		msgbox "Select an avatar nickname from the Avatar name list box.", vbInformation, "Microsoft Virtual Worlds"
		AvatarList.focus
	'Otherwise, set cookie and connect
	else
		AvatarAccessControl.DisconnectAvatar
		document.cookie = "sUser=" & AvatarList.value & ";path=/;expires=Fri, 1 Jan 2038 00:00:00 GMT"
		document.cookie = "sWorld=" & curWorldname & ";path=/;expires=Fri, 1 Jan 2038 00:00:00 GMT"
		if document.body.clientHeight < 475 then
			document.cookie = "sLogoffURL=vwStartClient.htm;path=/;expires=Fri, 1 Jan 2038 00:00:00 GMT"
		else
			document.cookie = "sLogoffURL=vwStartSDK.htm;path=/;expires=Fri, 1 Jan 2038 00:00:00 GMT"
		end if
		window.location.href= "Client.htm" '''', "_blank", "toolbar=no,location=no,menubar=no,status=yes,resizable=yes"
		'window.close
	end if
end sub

' ===========================================
' OnConnectToNewWorld
' Connect avatar to new world,
' Do extra checking for valid servername/worldname

sub OnConnectToNewWorld
	dim curWorldname
	curWorldname = trim(NewWorld.Value)

	'First, check for valid avatar and world name
	if CheckAvatarListValue = false then
		msgbox "Select an avatar nickname from the Avatar name list box.", vbInformation, "Microsoft Virtual Worlds"
		AvatarList.focus
	elseif (curWorldname = "") or (curWorldname =  "Pick world or type in new one") then
		msgbox "Enter a destination.", vbInformation, "Microsoft Virtual Worlds"
		NewWorld.Focus
	elseif len(curWorldname) > 250 then
		msgbox "The maximum length for a world name was exceeded. Re-enter a world name.", vbExclamation, "Microsoft Virtual Worlds"
		NewWorld.Focus
	elseif not (theClient.vwclient.world.validateworldname(curWorldname)) then
		msgbox "A world name can not contain a file extension, a space, a tab, or any of the following characters:" & vbcrlf & " : ? "" < > | ! & ' ," & vbcrlf & "Enter a new world name.", vbExclamation, "Microsoft Virtual Worlds"
		NewWorld.Focus
	elseif instr(curWorldName, "/") = 0 and instr(curWorldName, "\") = 0 then
		msgbox "A world name must be in the form of ServerName/WorldName.", vbExclamation, "Microsoft Virtual Worlds"
	'Otherwise, set cookie and connect
	else
		'AvatarAccessControl.DisconnectAvatar
		document.cookie = "sUser=" & AvatarList.value & ";path=/;expires=Fri, 1 Jan 2038 00:00:00 GMT"
		document.cookie = "sWorld=" & curWorldname & ";path=/;expires=Fri, 1 Jan 2038 00:00:00 GMT"
		if document.body.clientHeight < 475 then
			document.cookie = "sLogoffURL=vwStartClient.htm;path=/;expires=Fri, 1 Jan 2038 00:00:00 GMT"
		else
			document.cookie = "sLogoffURL=vwStartSDK.htm;path=/;expires=Fri, 1 Jan 2038 00:00:00 GMT"
		end if
		window.location.href= "Client.htm"   '', "_blank", "toolbar=no,location=no,menubar=no,status=yes,resizable=yes"
		'window.close
	end if	

end sub

' ===========================================
' checkAvatarListValue
' Check to make sure current avatar isn't (new avatar)
function checkAvatarListValue
	if trim(AvatarList.value) <> "(new avatar)" and trim(AvatarList.value) <> "" then
		checkAvatarListValue = true
	else
		checkAvatarListValue = false
	end if
end function

'===========================================
' Terms_OnClick
' This launches a modal dialog box containing the terms of use
' If it is changed to be in the same window as login, client.htm
' will have to launch a frameset that switches out login.htm and
' terms.htm
sub Terms_OnClick
	dim retDlg
	window.event.returnValue = false
	retDlg = window.showModalDialog("../shared/terms.htm", window, "dialogHeight:210px;dialogWidth:300px;status:no;help:no")
end sub

sub vwACK_onclick
	window.event.returnValue = false
	window.open "..\..\ack\html\ack.htm", "ACK", "toolbar=no,location=no,status=no,menubar=no,resizable=no,width=680,height=502,top=" & (screen.availHeight - 502) / 2 & ",left=" & (screen.availWidth - 680) /2
end sub

sub vwWorldWizard_onclick
	window.event.returnValue = false
	window.open "..\..\sdk\wizards\world\main.htm", "ACK", "toolbar=no,location=no,status=no,menubar=no,resizable=no,width=680,height=502,top=" & (screen.availHeight - 502) / 2 & ",left=" & (screen.availWidth - 680) /2
end sub
'===========================================
' OnHelp
sub OnHelp()
	dim helpString
	dim global
	set global = theclient.vwclient.world.global
	window.event.returnValue = false
	if Global.IsValidProperty("HelpPath") then
		helpString = "mk:@MSITStore:" & global.HelpPath
		helpString = helpString & "vworlds.chm::/UIRef/cliels/loginscr.htm>sdkall"
		window.showhelp helpString
	else
	end if
end sub

function PosInList(tagName, sItem)
	dim i
	PosInList = tagName.length
	for i = 0 to tagName.length - 1
		if lcase(sItem) < lcase(tagName.options.item(i).text) then
			PosInList = i
			exit for
		end if
	next
end function

sub document_onmouseover
	select case window.event.srcElement.className
	case "bEnabled"
		window.event.srcElement.className = "bHighlight"
	case "vHelp"
		imgHelp.src = "..\shared\helpH.gif"
	case "vGo"
		window.event.srcElement.src = "..\shared\go1.gif"
	case "vMenuStart"
		window.event.srcElement.className = "vwMenuHighlight"
	case else
	end select
end sub

sub document_onmouseout
	select case window.event.srcElement.className
	case "bHighlight"
		window.event.srcElement.className = "bEnabled"
	case "vHelp"
		imgHelp.src = "..\shared\help.gif"
	case "vGo"
		window.event.srcElement.src = "..\shared\go2.gif"
	case "vwMenuHighlight"
		window.event.srcElement.className = "vMenuStart"
	case else
	end select
end sub

function GetRenderControl()
	set GetRenderControl = Nothing
end function

sub HandleUpdateOptions
	' First check if cookies are turned on
	if navigator.cookieEnabled then
		document.cookie = "iUseHWAcceleration=" & iUseHWAcceleration & ";path=/;expires=Fri, 1 Jan 2038 00:00:00 GMT"
		document.cookie = "bForceBackBuffering=" + bForceBackBuffering + ";path=/;expires=Fri, 1 Jan 2038 00:00:00 GMT;"
		document.cookie = "iDrawFastWhenMoving=" & iDrawFastWhenMoving & ";path=/;expires=Fri, 1 Jan 2038 00:00:00 GMT"
		document.cookie = "iColorModel=" & iColorModel & ";path=/;expires=Fri, 1 Jan 2038 00:00:00 GMT"
		document.cookie = "bAutoDownloadAvatars=" & bAutoDownloadAvatars & ";path=/;expires=Fri, 1 Jan 2038 00:00:00 GMT"
		document.cookie = "bPlaySoundFX=" & bPlaySoundFX & ";path=/;expires=Fri, 1 Jan 2038 00:00:00 GMT"
		document.cookie = "bShowGestures=" & bShowGestures & ";path=/;expires=Fri, 1 Jan 2038 00:00:00 GMT"
		document.cookie = "bShowEvents=" & bShowEvents & ";path=/;expires=Fri, 1 Jan 2038 00:00:00 GMT"
		document.cookie = "bShowSysMsgs=" & bShowSysMsgs & ";path=/;expires=Fri, 1 Jan 2038 00:00:00 GMT"
	end if
end sub

' Cookie functions
' This function gets the cookies when the page is first loaded
Sub LoadPrefsFromCookie
	dim cookie, cookieList
	' First check if cookies are turned on
	if navigator.cookieEnabled then
		cookieList = parseCookie
		for each cookie in cookieList
			select case cookie(0)
			case "iUseHWAcceleration"
				if IsValidCookie(cookie(1))then
					iUseHWAcceleration = cookie(1)
				end if
			case "bForceBackBuffering"
				if IsValidCookie(cookie(1))then
					bForceBackBuffering = cookie(1)
				end if
			case "iDrawFastWhenMoving"
				if IsValidCookie(cookie(1))then
					iDrawFastWhenMoving = cookie(1)
				end if
			case "iColorModel"
				if IsValidCookie(cookie(1))then
					iColorModel = cookie(1)
				end if
			case "bAutoDownloadAvatars"
				if IsValidCookie(cookie(1))then
					bAutoDownloadAvatars = cookie(1)
				end if
			case "bPlaySoundFX"
				if IsValidCookie(cookie(1))then
					bPlaySoundFX = cookie(1)
				end if
			case "bShowGestures"
				if IsValidCookie(cookie(1))then
					bShowGestures = cookie(1)
				end if
			case "bShowEvents"
				if IsValidCookie(cookie(1))then
					bShowEvents = cookie(1)
				end if
			case "bShowSysMsgs"
				if IsValidCookie(cookie(1))then
					bShowSysMsgs = cookie(1)
				end if
			case "sUser"
				if IsValidCookie(cookie(1))then
					AvatarList.value = cookie(1)
					AvatarList_OnChange
				end if
			case else
			end select
		next
	end if
end sub

' splits a cookie. returns an array
function parseCookie
	on error resume next
	dim cookie, cookieList, i, nCookies()
	' Separate each cookie.
	cookieList = split(document.cookie, ";")
	redim nCookies(ubound(cookieList))
	for i = 0 to ubound(cookieList)
		redim nCookie(1)
		cookie = split(cookieList(i), "=")
		nCookie(0) = trim(cookie(0))
		nCookie(1) = trim(cookie(1))
		nCookies(i) = nCookie
	next
	parseCookie = nCookies
end function

'---------------------------------------------------------
' IsValidCookie
' Check that cookie hasn't gotten messed up as a result
' of a crash in VWorlds [reena]

function IsValidCookie(cookie)
	if VarType(cookie) = vbEmpty or VarType(cookie) = vbNull or VarType(cookie) = vbError then
		IsValidCookie = false
	else
		IsValidCookie = true
	end if
end function

sub document_OnSelectStart
	Select Case window.event.srcElement.tagName
	case "INPUT", "TEXTAREA"
		select case window.event.srcElement.type
		case "text", "textarea", "file", "password"
		case else
			window.event.returnValue = false
		end select
	case Else
		window.event.returnValue = false
	end select
end sub

</script>
<SCRIPT LANGUAGE="JScript" SRC="CheckNames.htm"></SCRIPT>

</head>
<body style=background-color:white;>
	<img style=position:absolute;left:0;top:0; src=start.gif>
	<div class=vMainTitle style=left:70;top:95;>
		Welcome to Microsoft Virtual Worlds
	</div>
		<div class=vH2 style=font-weight:normal;color:#529cab;left:75;top:124;>
		Developed by the Virtual Worlds Group at Microsoft Research<br>
		to advance the study of shared virtual environments.
	</div>
	<img style=position:absolute;left:55;top:167; src=startDot.gif>
	<div class=vLabel style=left:75;top:165;font-size:10pt;>
		Log on to an existing Virtual World.
	</div>
	<div class=vLabel style=left:90;top:195;>
		Avatar name:
	</div>
	<select id=AvatarList class=vInput style=left:180;top:192;width:170;><option>&nbsp;</option></select>
	<button class=bEnabled id=btnDelete onclick=OnDeleteAvatar style=left:360;top:191;width:69;>Delete</button>

	<div class=vLabel style=left:90;top:224;>
		Past Worlds
	</div>
	
	<select id=LastWorld class=vInput style=position:absolute;height:21;width:250;left:180;top:219;><option>&nbsp;</option></select>
	<img onclick=OnConnect class=vGo id=imgGo src=../shared/go2.gif style=left:440;top:217;">

	<div class=vLabel style=left:104;top:236;>
		- or -
	</div>
	<div class=vLabel style=left:90;top:250;>
		New World
	</div>
	<input id=NewWorld class=vInput style=position:absolute;height:21;width:250;left:180;top:245;type=text value="(Enter servername/worldname here)"></input>
	<img onclick=OnConnectToNewWorld  class=vGo  id=imgGo2 src=../shared/go2.gif style=left:440;top:243;></input>

		
	<button class=bEnabled id=btnDirectory onclick=OnDirectory style=left:180;top:275;width:120;>Worlds...</button>
	<button class=bEnabled id=btnOptions onclick=OnOptions style=left:309;top:275;width:120;>Options...</button>

	<img id=divWWBullet style=position:absolute;left:55;top:302; src=startDot.gif>
	<div id=divWorldWizard class=vLabel style=left:75;top:300;font-size:10pt;>
		Create a new world using the <a id=vwWorldWizard href="" class=vMenuStart>Create a World Wizard</a>.
	</div>

	<img id=divACKBullet style=position:absolute;left:55;top:332; src=startDot.gif>
	<div id=divACK class=vLabel style=left:75;top:330;font-size:10pt;>
		Create a custom avatar using the <a id=vwACK href="" class=vMenuStart>Avatar Construction Kit</a>.
	</div>
	<script language=vbs>
		if document.body.clientHeight < 475 then
			divACKBullet.style.display = "none"
			divACK.style.display = "none"
			divWWBullet.style.display = "none"
			divWorldWizard.style.display = "none"
		end if
	</script>
	<img id=divWebBullet style=position:absolute;left:55;top:362; src=startDot.gif>
	<div id=divWeb class=vLabel style=left:75;top:360;font-size:10pt;>
		Or visit the <a href="http://vworlds.research.microsoft.com" target=_default class=vMenuStart>Virtual Worlds</a> external web site.
	</div>
	<div id=divSignature class=vLabel style=left:75;top:395;font-size:10pt;>
		Enjoy!<br>
		The Microsoft Virtual Worlds team.
	</div>
	<a id=lnkHelp onclick=OnHelp href="">
		<img src=../shared/help.gif id=imgHelp class=vHelp style=top:449;>
	</a>
	<div id=Copyright class=vCopyTerms style=left:130;top:454;>
		&#169;1998-2000 Microsoft Corporation. All rights reserved. <a id=Terms class=vMenuStart href="" style=font-size:7pt;>Terms of Use.</a>
	</div>
	<script language=vbs>
		if document.body.clientHeight < 475 then
			divWebBullet.style.top = 302
			divWeb.style.top = 300
			divSignature.style.top = 325
			imgHelp.style.top = 379
			Copyright.style.top = 383
		end if
	</script>
</body>
</html>